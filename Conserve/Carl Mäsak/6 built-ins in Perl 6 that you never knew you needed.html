http://strangelyconsistent.org/blog/6-builtins-in-perl-6-that-you-never-knew-you-needed
<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <meta name=viewport content="width=device-width, initial-scale=1">
    <title>6 built-ins in Perl 6 that you never knew you needed :: Strangely Consistent</title>
    <!--[if lt IE 9]>
      <script src="http://html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->
    <link rel="alternate" title="Strangely Consistent atom feed" href="http://strangelyconsistent.org/blog/feed.atom" type="application/atom+xml">
    <link href='http://fonts.googleapis.com/css?family=OFL+Sorts+Mill+Goudy+TT:italic' rel='stylesheet' type='text/css'>
    <link href='http://fonts.googleapis.com/css?family=Molengo' rel='stylesheet' type='text/css'>
    <style type="text/css" media="all"> 
      @import "http://strangelyconsistent.org/blog/css/main.css";
    </style>
  </head>
  <body>
    <header>
      <hgroup>
        <h1><a href="/">Strangely Consistent</a></h1>
        <h2>Theory, practice, and languages, braided together</h2>
      </hgroup>
      <nav>
        <ul>
          <li><a href="/">Home</a></li>
          <li><a href="/about">About</a></li>
          <li><a href="http://strangelyconsistent.org/blog/list-of-posts">Archive</a></li>
        </ul>
      </nav>
    </header>

    <article>
  <header>
    <div class="postinfo">
      <div><time datetime="2010-07-23T03:24:00+02:00" pubdate="pubdate">23 Jul, 2010</time></div>
      <div>by Carl Mäsak</div>
      <div><a href="/no-comments">no notes</a></div>
    </div>
    <h1><a href="http://strangelyconsistent.org/blog/6-builtins-in-perl-6-that-you-never-knew-you-needed">6 built-ins in Perl 6 that you never knew you needed</a></h1>
  </header>

  <h2>.pick and .roll</h2>

<pre><code>say @deck.pick();                   # pick a card, any card...

say @deck.pick(5);                  # poker hand

my @shuffled = @deck.pick(*);       # here, '*' means 'keep going'

my @urn = &lt;black white white&gt;;      # beads, 1/3 black, 2/3 white
.say for @urn.roll(50);             # like .pick, but new each time

for @urn.roll(*) {
    .say;                           # infinite bead selector
}

say [+] (1..6).roll(4);             # 4d6

class Enemy {
    method attack-with-arrows   { say "peow peow peow" }
    method attack-with-swords   { say "swish cling clang" }
    method attack-with-fireball { say "sssSSS fwoooof" }
    method attack-with-camelia  { say "flap flap RAWWR!" }
}

my $selector = { .name ~~ /^ 'attack-with-' / };
given Enemy.new -&gt; $e {
    my $attack-strategy
        = $e.^methods().grep($selector).pick();

    $e.$attack-strategy();           # call a random method
}
</code></pre>

<h2>.classify</h2>

<pre><code>my @names = &lt;Patrick Jonathan Larry Moritz Audrey&gt;;
say .key, "\t", ~.values
    for @names.classify( *.chars );

# Output:
# 5       Larry
# 6       Moritz Audrey
# 7       Patrick
# 8       Jonathan

.say for slurp("README")\            # whole file into string
         .words()\                   # split into list of words
         .classify( *.Str )\         # group words w/ multiplicity
         .map({; .key =&gt; .value.elems })\
                                     # turn lists into lengths
         .sort( { -.value } )\       # sort descending
         .[ ^10 ];                   # 10 most common words

class Student {
    has Str $.name;
    has Int $.grade is rw;
}

my Student @students = get-students();
for @students.classify( *.grade ).sort -&gt; $group {
    say "These students got grade $group.key():";
    say .name for $group.value.list;
}
</code></pre>

<h2>.sort</h2>

<pre><code># 1 if $a is higher, -1 if $b is higher, 0 if equal
$a &lt;=&gt; $b;

# sort students according to grade
@students.sort: -&gt; $a, $b { $a.grade &lt;=&gt; $b.grade };

# same thing
@students.sort: { $^a.grade &lt;=&gt; $^b.grade };

# same thing
@students.sort: { .grade };

# same thing
@students.sort: *.grade;

# leg gives -1, 0 or 1 according to lexicographic ordering
# 'leg' is for Str, 'cmp' is now for type-agnostic sort
$a leg $b;

# sort students by name (Unicode order)
@students.sort: { $^a.name leg $^b.name };

# same thing
@students.sort: *.name;

# don't worry, things are properly cached; no re-evaluations
@items.sort: *.expensive-calculation();

# ...which means this works (and is a fair shuffle)
@deck.sort: { rand }

# ...but this is cuter :)
@deck.pick(*);
</code></pre>

<h2>Operator overloading</h2>

<pre><code>sub infix:&lt;±&gt;($number, $fuzz) {
    $number - $fuzz + rand * 2 * $fuzz;
}

say 15 ± 5;                          # somewhere between 10 and 20

sub postfix:&lt;!&gt;($n) { [*] 1..$n }
say 5!;                              # 120

class Physical::Unit {
    has Int $.kg = 0;                # these attrs denote powers of units
    has Int $.m  = 0;                # eg $.kg == 2 means that this object
    has Int $.s  = 0;                # has a kg**2 unit

    has Numeric $.payload;

    method multiply(Physical::Unit $other) {
        Physical::Unit.new(
            :kg( $.kg + $other.kg ),
            :m( $.m + $other.m ),
            :s( $.s + $other.s ),
            :payload( $.payload * $other.payload )
        )
    }

    method invert() {
        Physical::Unit.new(
            :kg( -$.kg ), :m( -$.m ), :s( -$.s ),
            :payload( 1 / $.payload )
        )
    }

    method Str {
        $.payload
        ~ ($.kg ?? ($.kg == 1 ?? " kg" !! "kg**$.kg") !! "")
        ~ ($.m  ?? ($.m  == 1 ?? " m"  !! "m**$.m")   !! "")
        ~ ($.s  ?? ($.s  == 1 ?? " s"  !! "s**$.s")   !! "")
    }
}

sub postfix:&lt;kg&gt;(Numeric $payload) { Physical::Unit.new( :kg(1), :$payload ) } 
sub postfix:&lt;m&gt;(Numeric $payload) { Physical::Unit.new( :m(1), :$payload ) }
sub postfix:&lt;s&gt;(Numeric $payload) { Physical::Unit.new( :s(1), :$payload ) }

# Note how we now use 'multi sub', so as not to shadow the original infix:&lt;*&gt;
multi sub infix:&lt;*&gt;(Physical::Unit $a, $b) {
    $a.clone( :payload($a.payload * $b) );
}

multi sub infix:&lt;*&gt;($a, Physical::Unit $b) {
    $b.clone( :payload($a * $b.payload) );
}

multi sub infix:&lt;*&gt;(Physical::Unit $a, Physical::Unit $b) {
    $a.multiply($b);
}

multi sub infix:&lt;/&gt;(Physical::Unit $a, $b) {
    $a.clone( :payload($a.payload / $b) );
}

multi sub infix:&lt;/&gt;($a, Physical::Unit $b) {
    $b.invert.clone( :payload($a / $b.payload) );
}

multi sub infix:&lt;/&gt;(Physical::Unit $a, Physical::Unit $b) {
    $a.multiply($b.invert);
}

say 5m / 2s;                         # 2.5 m s**-1
say 100kg * 2m / 5s;                 # 40 kg m s**-1
</code></pre>

<h2>infix:&lt;Z&gt;</h2>

<pre><code># Z (the 'zip operator') means "mix these lists together"
my @tastes = &lt;spicy sweet bland&gt;;
my @foods = &lt;soup potatoes tofu&gt;;
@tastes Z @foods; # &lt;spicy soup sweet potatoes bland tofu&gt;

# » means "call the method for each element"
.say for @students».grade;                 # all the grades

for @students».name Z @students».grade -&gt; $name, $grade {
    say "$name got a $grade this year";
}

# Note that the latter list is infinite -- it works anyway
for @students».name Z (1..6).roll(*) -&gt; $name, $roll {
    say "$name rolls a $roll";
}

# you can also Z together two lists with an infix op
my @total-scores = @first-scores Z+ @second-scores;

# strings as keys, the appropriate number of 1s as values
my %hash = @names Z=&gt; 1 xx *;              # xx is list repeat

# line people up with increasing numbers
my %people2numbers = @people Z=&gt; 1..*;

# don't have a good op? roll your own!
sub infix:&lt;likes&gt;($liker, $likee) { "$liker is fond of $likee" }
# note how the op infix:&lt;Zlikes&gt; is automatically available
my @relations = @likers Zlikes @likees;
</code></pre>

<h2>infix:&lt;...&gt;</h2>

<pre><code>1 ... $n                                    # integers 1 to $n
$n ... 1                                    # and backwards

1, 3 ... $n                                 # odd numbers to $n
1, 3, ... *                                 # odd numbers
1, 2, 4 ... *                               # powers of two
map { $_ * $_ }, (1 ... *)                  # squares

1, 1, -&gt; $a, $b { $a + $b } ... *           # fibonacci
1, 1, { $^a + $^b } ... *                   # ditto
1, 1, *+* ... *                             # ditto

'Camelia', *.chop ... '';                   # all prefixes of 'Camelia'

# See http://blog.plover.com/CS/parentheses.html
# for the principle behind this
sub next-balanced-paren-string($s) {
    $s ~~ /^ ( '('+ ) ( ')'+ ) '(' /;
    [~] $s.substr(0, $/.from),
        "()" x ($1.chars - 1),
        "(" x ($0.chars - $1.chars + 2),
        ")",
        $s.substr($/.to);
}

my $N = 3;

my $start = "()" x $N;
my &amp;step = &amp;next-balanced-paren-string;
my $end = "(" x $N ~ ")" x $N;

for $start, &amp;step ... $end -&gt; $string {
    say $string;
}

# Output:
# ()()()
# (())()
# ()(())
# (()())
# ((()))
</code></pre>

<p>Rakudo Star releases in a week, July 29th.</p>

</article>

  </body>
</html>
